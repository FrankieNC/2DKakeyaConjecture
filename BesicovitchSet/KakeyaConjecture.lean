/-
Copyright (c) 2025 Francesco Nishanil Chotuck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Francesco Nishanil Chotuck, Bhavik Mehta
-/

import BesicovitchSet.KakeyaSet
import Mathlib.Topology.MetricSpace.HausdorffDimension
import Mathlib.MeasureTheory.Measure.Hausdorff
import Mathlib.Order.CompletePartialOrder
import Mathlib

/-!
# Kakeya in one and two dimensions

This file collects basic results and conjectural statements about Kakeya sets
in low dimensions.


## Main statements

* In one dimension, any Kakeya set has full Hausdorff dimension:
  `dimH K = 1`. (Section `Kakeya1D`.)

* We also formalise a key technical result required for the proof:
  `exists_GŒ¥_of_dimH`, which shows that any subset of `‚Ñù‚Åø` can be thickened to a
  `GŒ¥` set of the same Hausdorff dimension.

## References

TO DO
-/

open Set Topology Real NNReal ENNReal MeasureTheory Measure Filter EMetric MetricSpace Metric

namespace Kakeya1D

/-- Any Kakeya set in `‚Ñù` contains a closed unit‚Äêlength interval. -/
lemma kakeya_contains_unit_Icc {K : Set ‚Ñù} (hK : IsKakeya K) :
    ‚àÉ x‚ÇÄ, Icc x‚ÇÄ (x‚ÇÄ + 1) ‚äÜ K := by
  have := hK 1 (by simp)
  rcases this with ‚ü®x‚ÇÄ, hseg‚ü©
  exact ‚ü®x‚ÇÄ, by simpa using hseg‚ü©

/-- Any closed interval of length 1 has Hausdorff dimension 1. -/
lemma dimH_Icc_length_one (a : ‚Ñù) :
    dimH (Icc a (a + 1)) = 1 := by
  have h : (interior (Icc a (a + 1))).Nonempty := by simp [interior_Icc]
  calc
    dimH (Icc a (a + 1)) = Module.finrank ‚Ñù ‚Ñù := Real.dimH_of_nonempty_interior h
    _ = 1 := by simp

/-- If a set contains some unit interval, then its `dimH ‚â• 1`. -/
lemma dimH_of_contains_Icc {K : Set ‚Ñù} {x‚ÇÄ} (h : Icc x‚ÇÄ (x‚ÇÄ + 1) ‚äÜ K) :
    1 ‚â§ dimH K := by
  calc
    1 = dimH (Icc x‚ÇÄ (x‚ÇÄ + 1)) := (dimH_Icc_length_one x‚ÇÄ).symm
    _ ‚â§ dimH K := dimH_mono h

/-- Any subset of `‚Ñù` has `dimH ‚â§ 1`. -/
lemma dimH_le_one_univ : ‚àÄ (K : Set ‚Ñù), dimH K ‚â§ 1 := fun K ‚Ü¶ by
  calc
    dimH K ‚â§ dimH (univ : Set ‚Ñù) := dimH_mono (subset_univ _)
    _ = Module.finrank ‚Ñù ‚Ñù := by simp [dimH_univ]
    _ = 1 := by simp

/-- Any Kakeya set in `‚Ñù` has full Hausdorff dimension. -/
theorem isKakeya.dimH_eq_one (K : Set ‚Ñù) (hK : IsKakeya K) :
    dimH K = 1 := by
  rcases kakeya_contains_unit_Icc hK with ‚ü®x‚ÇÄ, hsub‚ü©
  exact le_antisymm (dimH_le_one_univ K) (dimH_of_contains_Icc hsub)

end Kakeya1D

namespace Kakeya2D

-- The following lemmas were formalised by Bhavik Mehta as part of the proof of
-- `exists_GŒ¥_superset_hausdorffMeasure_eq`. At the time of writing they are not
-- yet included in Mathlib.

lemma le_of_forall_mul_le {a b : ‚Ñù‚â•0‚àû} (h : ‚àÄ Œµ : ‚Ñù‚â•0, 1 < Œµ ‚Üí a ‚â§ b * Œµ) : a ‚â§ b := by
  obtain rfl | ha := eq_or_ne a ‚ä§
  ¬∑ have : b * 2 = ‚ä§ := by
      specialize h 2
      norm_num at h
      assumption
    by_contra! hb
    exact ENNReal.mul_ne_top hb.ne (by simp) this
  apply le_of_forall_pos_nnreal_lt
  intro r hr hra
  generalize hŒµ : a / r = Œµ
  have hŒµ' : Œµ ‚â† ‚ä§ := by
    rw [‚Üê hŒµ]
    exact (ENNReal.div_lt_top ha (by positivity)).ne
  lift Œµ to ‚Ñù‚â•0 using hŒµ'
  have hŒµ' : 1 < Œµ := by
    suffices (1 : ‚Ñù‚â•0‚àû) < Œµ by simpa
    rwa [‚Üê hŒµ, ENNReal.lt_div_iff_mul_lt (by simp) (by simp), one_mul]
  have : a / Œµ = r := by
    rw [‚Üê hŒµ, ENNReal.div_div_cancel _ ha]
    exact (hra.trans_le' (by simp)).ne'
  rw [‚Üê this]
  apply ENNReal.div_le_of_le_mul
  exact h Œµ hŒµ'

variable {X : Type*} [EMetricSpace X]

/-- The infimum of `‚àë diam (t n) ^ d` over all countable
covers `(t n)` of `S` by sets in `p` with `diam (t n) ‚â§ r`.-/
noncomputable def deltaHausdorffWith (p : Set (Set X)) (d : ‚Ñù) (r : ‚Ñù‚â•0‚àû) (S : Set X) : ‚Ñù‚â•0‚àû :=
  ‚®Ö (t : ‚Ñï ‚Üí Set X) (_ : S ‚äÜ ‚ãÉ n, t n) (_ : ‚àÄ n, diam (t n) ‚â§ r) (_ : ‚àÄ n, t n ‚àà p),
    ‚àë' n, ‚®Ü _ : (t n).Nonempty, diam (t n) ^ d

lemma deltaHausdorffWith_antitone_prop {r : ‚Ñù‚â•0‚àû} {d : ‚Ñù} {S : Set X} :
    Antitone (deltaHausdorffWith ¬∑ d r S) :=
  fun _ _ hp ‚Ü¶ iInf‚ÇÇ_mono fun _ _ ‚Ü¶ iInf‚ÇÇ_mono' fun hU' hU'' ‚Ü¶ ‚ü®hU', fun n ‚Ü¶ hp (hU'' n), le_rfl‚ü©

lemma deltaHausdorffWith_antitone {p : Set (Set X)} {d : ‚Ñù} {S : Set X} :
    Antitone (deltaHausdorffWith p d ¬∑ S) :=
  fun _ _ hr ‚Ü¶ iInf‚ÇÇ_mono fun _ _ ‚Ü¶ iInf‚ÇÇ_mono' fun hU' hU'' ‚Ü¶
    ‚ü®fun n ‚Ü¶ (hU' n).trans hr, hU'', le_rfl‚ü©

lemma iSup‚ÇÇ_nnreal_deltaHausdorffWith (p : Set (Set X)) (d : ‚Ñù) (S : Set X) :
    ‚®Ü (r : ‚Ñù‚â•0) (_ : 0 < r), deltaHausdorffWith p d r S =
      ‚®Ü (r : ‚Ñù‚â•0‚àû) (_ : 0 < r), deltaHausdorffWith p d r S := by
  refine le_antisymm (iSup‚ÇÇ_mono' ?_) (iSup‚ÇÇ_mono' ?_)
  ¬∑ intro r hr
    exact ‚ü®r, by simpa‚ü©
  ¬∑ intro r hr
    obtain rfl | hr' := eq_or_ne r ‚ä§
    ¬∑ refine ‚ü®1, by simp, ?_‚ü©
      apply deltaHausdorffWith_antitone
      simp
    lift r to ‚Ñù‚â•0 using hr'
    exact ‚ü®r, by simpa using hr‚ü©

lemma exists_isOpen_diam {E : Set X} (hE : E.Nontrivial) {Œ¥ : ‚Ñù‚â•0} (hŒ¥ : 1 < Œ¥) :
    ‚àÉ U, E ‚äÜ U ‚àß IsOpen U ‚àß diam U ‚â§ Œ¥ * diam E := by
  have hŒ¥‚ÇÄ : Œ¥ ‚â† 0 := by positivity
  obtain hE | hne := eq_or_ne (diam E) ‚ä§
  ¬∑ use Set.univ
    simp [hE, hŒ¥‚ÇÄ]
  lift diam E to ‚Ñù‚â•0 using hne with dE hdE
  have hdE' : 0 < dE := by rwa [‚Üê EMetric.diam_pos_iff, ‚Üê hdE, ENNReal.coe_pos] at hE
  let Œµ : ‚Ñù‚â•0 := (Œ¥ - 1) / 2 * dE
  have hŒµ : 0 < Œµ := by
    have : 0 < Œ¥ - 1 := by simpa
    positivity
  refine ‚ü®Metric.thickening Œµ E, Metric.self_subset_thickening (by simpa) _,
    Metric.isOpen_thickening, ?_‚ü©
  calc diam (Metric.thickening Œµ E) ‚â§ diam E + 2 * Œµ := Metric.ediam_thickening_le _
    _ = dE + 2 * Œµ := by rw [hdE]
    _ = dE + 2 * ‚Üë((Œ¥ - 1) / 2 * dE) := rfl
  norm_cast
  rw [‚Üê mul_assoc, ‚Üê one_add_mul, mul_div_cancel‚ÇÄ _ (by simp), add_tsub_cancel_of_le hŒ¥.le]

lemma deltaHausdorffWith_isOpen (S : Set X) (d : ‚Ñù) (r : ‚Ñù‚â•0) (Œµ : ‚Ñù‚â•0)
    (hr : 0 < r) (hŒµ : 1 < Œµ) (hd : 0 ‚â§ d) :
    deltaHausdorffWith {U | IsOpen U} d (Œµ * r) S ‚â§ Œµ ^ d * deltaHausdorffWith Set.univ d r S := by
  apply ENNReal.le_of_forall_pos_le_add
  intro ŒΩ hŒΩ h
  have hŒµ‚ÇÄ : Œµ ‚â† 0 := by positivity
  simp only [deltaHausdorffWith, mul_iInf_of_ne (a := Œµ ^ d) (by simp [hŒµ‚ÇÄ]) (by simp [hŒµ‚ÇÄ]),
    iInf_add, ‚Üê ENNReal.tsum_mul_left, ENNReal.mul_iSup]
  refine iInf‚ÇÇ_mono' fun U hU ‚Ü¶ ?_
  obtain ‚ü®ŒΩ', hŒΩ', h'ŒΩ'‚ü© := ENNReal.exists_pos_sum_of_countable (Œµ := ŒΩ) (by simp [hŒΩ.ne']) ‚Ñï
  have U' (n : ‚Ñï) : ‚àÉ V, U n ‚äÜ V ‚àß IsOpen V ‚àß (V.Nonempty ‚Üí (U n).Nonempty) ‚àß
      (diam V ‚â§ Œµ * diam (U n) ‚à® diam (U n) = 0 ‚àß diam V ‚â§ min (Œµ * r) (ŒΩ' n ^ d‚Åª¬π)) := by
    obtain h' | h' := (U n).eq_empty_or_nonempty
    ¬∑ use ‚àÖ
      simp [h']
    obtain ‚ü®x, hx‚ü© | h' := h'.exists_eq_singleton_or_nontrivial
    ¬∑ refine ‚ü®EMetric.ball x (min (Œµ * r) (ŒΩ' n ^ d‚Åª¬π) / 2), ?_, ?_, by simp [hx], Or.inr ?_‚ü©
      ¬∑ rw [hx, Set.singleton_subset_iff]
        apply EMetric.mem_ball_self
        have : 0 < ŒΩ' n ^ d‚Åª¬π := NNReal.rpow_pos (hŒΩ' n)
        refine ENNReal.div_pos ?_ (by simp)
        positivity
      ¬∑ exact isOpen_ball
      ¬∑ simp only [hx, EMetric.diam_singleton, true_and]
        refine diam_ball.trans_eq ?_
        rw [ENNReal.mul_div_cancel (by simp) (by simp)]
    obtain ‚ü®V, hV, hV', hV''‚ü© := exists_isOpen_diam h' hŒµ
    exact ‚ü®V, hV, hV', by simp [h'.nonempty], Or.inl hV''‚ü©
  choose V hUV hVopen hVne hVdiam using U'
  use V, hU.trans (Set.iUnion_mono hUV)
  refine iInf‚ÇÇ_mono' fun h'U h''U ‚Ü¶ ?_
  have hVdiam_r (n : ‚Ñï) : diam (V n) ‚â§ Œµ * r := by
    obtain h | h := hVdiam n
    ¬∑ exact h.trans (mul_le_mul_left' (h'U n) Œµ)
    ¬∑ exact h.2.trans (by simp)
  use hVdiam_r, hVopen
  have h‚ÇÅ : ‚àë' (i : ‚Ñï), ((‚®Ü (_ : (U i).Nonempty), Œµ ^ d * diam (U i) ^ d) + ŒΩ' i) ‚â§
      (‚àë' (i : ‚Ñï), ‚®Ü (_ : (U i).Nonempty), Œµ ^ d * diam (U i) ^ d) + ŒΩ := by
    rw [ENNReal.tsum_add]
    gcongr
  refine h‚ÇÅ.trans' <| ENNReal.tsum_le_tsum fun n ‚Ü¶ ?_
  simp only [iSup_le_iff]
  intro hn
  simp only [hVne _ hn, iSup_pos]
  obtain h | ‚ü®h, h'‚ü© := hVdiam n
  ¬∑ calc diam (V n) ^ d ‚â§ (Œµ * diam (U n)) ^ d := by gcongr
      _ = Œµ ^ d * diam (U n) ^ d := by rw [ENNReal.mul_rpow_of_nonneg _ _ hd]
      _ ‚â§ _ := le_self_add
  ¬∑ obtain rfl | hd := hd.eq_or_lt
    ¬∑ simp
    calc diam (V n) ^ d ‚â§ (min (Œµ * r) (ŒΩ' n ^ d‚Åª¬π)) ^ d := by gcongr
      _ ‚â§ (ŒΩ' n ^ d‚Åª¬π : ‚Ñù‚â•0) ^ d := by gcongr; exact min_le_right _ _
      _ = ŒΩ' n := by
        rw [ENNReal.coe_rpow_of_nonneg, ENNReal.rpow_inv_rpow hd.ne']
        positivity
      _ ‚â§ _ := le_add_self

variable [MeasurableSpace X] [BorelSpace X]

lemma hausdorffMeasure_eq_iSup‚ÇÇ_deltaHausdorffWith {d : ‚Ñù} {S : Set X} :
    ŒºH[d] S = ‚®Ü (r : ‚Ñù‚â•0‚àû) (_ : 0 < r), deltaHausdorffWith Set.univ d r S := by
  simp only [hausdorffMeasure_apply, deltaHausdorffWith, Set.mem_univ, implies_true, iInf_pos]

lemma hausdorffMeasure_eq_iSup‚ÇÇ_deltaHausdorffWith_isOpen {d : ‚Ñù} {S : Set X} (hd : 0 ‚â§ d) :
    ŒºH[d] S = ‚®Ü (r : ‚Ñù‚â•0‚àû) (_ : 0 < r), deltaHausdorffWith {U | IsOpen U} d r S := by
  rw [hausdorffMeasure_eq_iSup‚ÇÇ_deltaHausdorffWith,
    ‚Üê iSup‚ÇÇ_nnreal_deltaHausdorffWith, ‚Üê iSup‚ÇÇ_nnreal_deltaHausdorffWith]
  refine le_antisymm (iSup‚ÇÇ_mono fun r hr ‚Ü¶ deltaHausdorffWith_antitone_prop (by simp)) ?_
  obtain rfl | hd := hd.eq_or_lt
  ¬∑ refine iSup‚ÇÇ_mono' fun r hr ‚Ü¶ ?_
    refine ‚ü®r / 2, by positivity, ?_‚ü©
    convert deltaHausdorffWith_isOpen S 0 (r / 2) 2 (by positivity) (by simp) le_rfl using 2
    ¬∑ norm_cast
      rw [mul_div_cancel‚ÇÄ _ (by simp)]
    simp
  apply le_of_forall_mul_le
  intro Œµ hŒµ
  rw [mul_comm]
  simp only [ENNReal.mul_iSup]
  refine iSup‚ÇÇ_mono' fun r hr ‚Ü¶ ?_
  have h‚ÇÇ : 0 < Œµ ^ (-d‚Åª¬π) * r := mul_pos (NNReal.rpow_pos (by positivity)) hr
  use Œµ ^ (-d‚Åª¬π) * r, h‚ÇÇ
  convert deltaHausdorffWith_isOpen S d _ (Œµ ^ d‚Åª¬π) h‚ÇÇ (NNReal.one_lt_rpow hŒµ (by positivity)) hd.le
  ¬∑ norm_cast
    rw [‚Üê mul_assoc, ‚Üê NNReal.rpow_add (by positivity)]
    simp
  ¬∑ rw [ENNReal.coe_rpow_of_nonneg _ (by positivity), ENNReal.rpow_inv_rpow hd.ne']

lemma tendsto_deltaHausdorffWith_isOpen_hausdorffMeasure {d : ‚Ñù} {S : Set X} (hd : 0 ‚â§ d) :
    Tendsto (deltaHausdorffWith {U | IsOpen U} d ¬∑ S) (ùìù[>] 0) (ùìù (ŒºH[d] S)) := by
  rw [hausdorffMeasure_eq_iSup‚ÇÇ_deltaHausdorffWith_isOpen hd]
  convert deltaHausdorffWith_antitone.tendsto_nhdsGT 0
  rw [sSup_image]
  simp

/-- For any set `E` and any non-negative `s : ‚Ñù`, there exists a `GŒ¥` set `G` containing `E`
with the same `s`-dimensional Hausdorff measure. -/
theorem exists_GŒ¥_superset_hausdorffMeasure_eq
    {X : Type*}
    [EMetricSpace X] [MeasurableSpace X] [BorelSpace X]
    {s : ‚Ñù} (hs : 0 ‚â§ s) (E : Set X) :
    ‚àÉ G : Set X, IsGŒ¥ G ‚àß E ‚äÜ G ‚àß ŒºH[s] G = ŒºH[s] E := by
  have h‚ÇÅ : ŒºH[s] E = ‚ä§ ‚à® ŒºH[s] E < ‚ä§ := by rw [‚Üê le_iff_eq_or_lt]; simp
  obtain h‚ÇÅ | h‚ÇÅ := h‚ÇÅ
  ¬∑ exact ‚ü®Set.univ, by simp, by simp, le_antisymm (by rw [h‚ÇÅ]; simp) (measure_mono (by simp))‚ü©
  obtain ‚ü®œÜ, h‚ÇÅœÜ, h‚ÇÇœÜ, h‚ÇÉœÜ‚ü© := exists_seq_strictAnti_tendsto' (show (0 : ‚Ñù‚â•0‚àû) < 1 by norm_num)
  have h‚ÇÑœÜ : Tendsto œÜ atTop (ùìù[>] 0) :=
    tendsto_nhdsWithin_mono_right
      (Set.range_subset_iff.2 (by simp_all)) (tendsto_nhdsWithin_range.2 h‚ÇÉœÜ)
  have hc (i : ‚Ñï) : ‚àÉ (U : ‚Ñï ‚Üí Set X) (hU : E ‚äÜ ‚ãÉ j, U j)
      (hU' : ‚àÄ j, diam (U j) ‚â§ œÜ i) (hU'' : ‚àÄ j, IsOpen (U j)),
      ‚àë' j, ‚®Ü (_ : (U j).Nonempty), diam (U j) ^ s <
        deltaHausdorffWith {U | IsOpen U} s (œÜ i) E + œÜ i := by
    have h‚ÇÇ :
        deltaHausdorffWith {U | IsOpen U} s (œÜ i) E <
          deltaHausdorffWith {U | IsOpen U} s (œÜ i) E + œÜ i := by
      apply ENNReal.lt_add_right _ _
      ¬∑ rw [‚Üê lt_top_iff_ne_top]
        refine h‚ÇÅ.trans_le' ?_
        rw [hausdorffMeasure_eq_iSup‚ÇÇ_deltaHausdorffWith_isOpen hs]
        exact le_iSup‚ÇÇ (Œ± := ‚Ñù‚â•0‚àû) _ (by simp_all)
      ¬∑ simp only [ne_eq, (h‚ÇÇœÜ i).1.ne', not_false_eq_true]
    conv_lhs at h‚ÇÇ =>
      simp only [deltaHausdorffWith]
    simp only [iInf_lt_iff, Set.mem_setOf_eq] at h‚ÇÇ
    exact h‚ÇÇ
  choose U hCov hDiam hOpen hU using hc
  let G : Set _ := ‚ãÇ i, ‚ãÉ j, U i j
  have hEG : E ‚äÜ G := Set.subset_iInter hCov
  have h‚ÇÅ (i : ‚Ñï) : deltaHausdorffWith {U | IsOpen U} s (œÜ i) G <
      deltaHausdorffWith {U | IsOpen U} s (œÜ i) E + œÜ i := by
    rw [deltaHausdorffWith]
    simp only [iInf_lt_iff]
    exact ‚ü®U i, Set.iInter_subset _ _, hDiam i, hOpen i, hU i‚ü©
  refine ‚ü®G, ?_, hEG, ?_‚ü©
  ¬∑ apply IsGŒ¥.iInter_of_isOpen
    intro i
    apply isOpen_iUnion
    intro j
    exact hOpen i j
  refine le_antisymm ?_ (measure_mono hEG)
  simpa using le_of_tendsto_of_tendsto'
    ((tendsto_deltaHausdorffWith_isOpen_hausdorffMeasure hs).comp h‚ÇÑœÜ)
    (((tendsto_deltaHausdorffWith_isOpen_hausdorffMeasure hs).comp h‚ÇÑœÜ).add h‚ÇÉœÜ) (fun i ‚Ü¶ (h‚ÇÅ i).le)

-- What follows is the contribution of Francesco Chotuck.

/-- A subset of `‚Ñù‚Åø` has finite Hausdorff dimension. -/
theorem dimH_lt_top {n : ‚Ñï} {A : Set (Fin n ‚Üí ‚Ñù)} :
    dimH A < ‚ä§ := by
  calc
    dimH A ‚â§ dimH (Set.univ : Set (Fin n ‚Üí ‚Ñù)) := dimH_mono (by simp)
    _ = n := dimH_univ_pi_fin n
    _ < ‚ä§ := by simp

theorem dimH_ne_top {n : ‚Ñï} {A : Set (Fin n ‚Üí ‚Ñù)} : dimH A ‚â† ‚ä§ := by
  simpa using (lt_top_iff_ne_top).1 dimH_lt_top

/-- For any subset `A` of `‚Ñù‚Åø` there is a `GŒ¥`‚Äêset `G` with `A ‚äÜ G` and `dimH G = dimH A`. -/
theorem exists_GŒ¥_of_dimH {n : ‚Ñï} (A : Set (Fin n ‚Üí ‚Ñù)) :
    ‚àÉ G : Set (Fin n ‚Üí ‚Ñù), IsGŒ¥ G ‚àß A ‚äÜ G ‚àß dimH G = dimH A := by
  generalize hA : dimH A = DA
  have : DA ‚â† ‚ä§ := Ne.symm (ne_of_ne_of_eq (id (Ne.symm dimH_ne_top)) hA)
  lift DA to ‚Ñù‚â•0 using this
  obtain ‚ü®œÜ, h‚ÇÅœÜ, h‚ÇÇœÜ, h‚ÇÉœÜ‚ü© := exists_seq_strictAnti_tendsto' (show (0 : ‚Ñù‚â•0) < 1 by norm_num)
  have h‚ÇÑœÜ : Tendsto œÜ atTop (ùìù[>] 0) :=
    tendsto_nhdsWithin_mono_right (Set.range_subset_iff.2 (by simp_all)) (tendsto_nhdsWithin_range.2 h‚ÇÉœÜ)
  choose G' hG'_GŒ¥ subG' meas_eq' using
    fun k : ‚Ñï ‚Ü¶ exists_GŒ¥_superset_hausdorffMeasure_eq (coe_nonneg (DA + œÜ k)) A
  let G := ‚ãÇ k, G' k
  have iGŒ¥ : IsGŒ¥ G := IsGŒ¥.iInter fun k ‚Ü¶ hG'_GŒ¥ k
  have Asub : A ‚äÜ G := subset_iInter fun k ‚Ü¶ subG' k
  have hge : dimH A ‚â§ dimH G := dimH_mono Asub
  have hle : dimH G ‚â§ dimH A := by
    apply dimH_le
    intro d' hd'
    by_contra! hgt
    have hd_pos : 0 < (d' : ‚Ñù‚â•0) - DA := by aesop
    rw [Metric.tendsto_atTop] at h‚ÇÉœÜ
    rcases h‚ÇÉœÜ _ hd_pos with ‚ü®k, hœÜk_lt‚ü©
    generalize hD : DA + œÜ k = D
    specialize h‚ÇÇœÜ k
    simp only [mem_Ioo] at h‚ÇÇœÜ
    cases' h‚ÇÇœÜ with hœÜk_gt_0 hœÜk_lt_1
    have hlt : DA < D := by
      linear_combination hD + hœÜk_gt_0
    have hŒºA : ŒºH[D] A = 0 := by
      apply hausdorffMeasure_of_dimH_lt
      rw [hA]
      norm_cast
    have hŒºGk : ŒºH[D] (G' k) = 0 := by
      rw [‚Üê hD, meas_eq', hD, hŒºA]
    have hmono : ŒºH[d'] G ‚â§ ŒºH[D] (G' k) := by
      calc
        ŒºH[d'] G ‚â§ ŒºH[d'] (G' k) := by
          apply measure_mono
          exact iInter_subset_of_subset k fun ‚¶Éa‚¶Ñ a ‚Ü¶ a
        _ ‚â§ ŒºH[D] (G' k) := by
          apply hausdorffMeasure_mono
          apply le_of_lt
          rw [‚Üê hD]
          simp only [NNReal.coe_add]
          specialize hœÜk_lt k
          norm_cast
          simp only [ge_iff_le, le_refl, val_eq_coe, dist_lt_coe, nndist_zero_eq_val',
            forall_const] at hœÜk_lt
          rw [lt_tsub_iff_left] at hœÜk_lt
          exact hœÜk_lt
    have h0 : ŒºH[d'] G = 0 := by
      have hbot : ŒºH[d'] G ‚â§ 0 := by
        apply hmono.trans_eq
        exact hŒºGk
      exact le_bot_iff.1 hbot
    simp [h0] at hd'
  rw [‚Üê hA]
  exact ‚ü®G, iGŒ¥, Asub, le_antisymm hle hge‚ü©

/-- If `A ‚äÜ ‚Ñù‚Åø` has finite `s`-dimensional Hausdorff measure,
    then for any `k ‚â§ s` and any `k`-plane `W`, the slices
    `A ‚à© (W·óÆ + x)` have finite `(s - k)`-measure for `ŒºH[k]`-almost all `x ‚àà W`. -/
theorem hausdorffMeasure_slicing
  (n : ‚Ñï)
  (A : Set (EuclideanSpace ‚Ñù (Fin n))) (hA : MeasurableSet A)
  (s : ‚Ñù) (hs : ŒºH[s] A < ‚ä§)
  (k : ‚Ñï) (hks : (k : ‚Ñù) ‚â§ s)
  (W : Submodule ‚Ñù (EuclideanSpace ‚Ñù (Fin n))) (hW : Module.finrank ‚Ñù W = k)
  (direction : W) (x : W) :
  ‚àÄ·µê x ‚àÇ (ŒºH[(k : ‚Ñù)]).restrict (W : Set (EuclideanSpace ‚Ñù (Fin n))),
    ŒºH[s - k] (A ‚à© (AffineSubspace.mk' x W.orthogonal : Set _)) < ‚ä§ := by
  sorry

end Kakeya2D
